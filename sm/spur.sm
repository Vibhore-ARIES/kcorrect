data "$!KCORRECT_DIR/data/etemplates/ematrix.default.dat"
define nt read 1 2 
define nb read 1 3 
lines 2 0
read <ein 1>

data "$!KCORRECT_DIR/data/etemplates/bmatrix.default.dat"
define nb read 1 2 
define nl read 1 3 
lines 2 0
read <bin 1>

data "$!KCORRECT_DIR/data/etemplates/lambda.default.dat"
lines 2 0
read <lambda 1>

set i = 0, $nb-1
define j <0>
while {$j<$nt} {
  set e$j = ein[$j*$nb+i]
  define j ($j+1)
}

set i = 0, $nl-1
define j <0>
while {$j<$nb} {
  set b$j = bin[$j*$nl+i]
  define j ($j+1)
}
set outlambda = 0.5*(lambda[i]+lambda[i+1])

set eout = e2+ 0.7*e3
define i <0>
declare bout $nl
while	{$i<$nb} {
  set	bout = bout+eout[$i]*b$i
  define i ($i+1)
}

define labellinesize 1.3
define boxlinesize 1.3
define linesize 1.3

set i = 0, dimen(bout)-1
set indx = i if (outlambda>2000. && outlambda<11000.)
set bout = bout[indx]
set outlambda = outlambda[indx]
#vecminmax bout boutmin boutmax
#set bout = bout/$boutmax
vecminmax bout boutmin boutmax
limits 2000. 11000. $boutmin $boutmax
putbox 1 2 0 4
connect outlambda bout		

ylab e_2(\lambda) + 0.7 e_3(\lambda)
xlab Wavelength (Angstroms)

data "$!KCORRECT_DIR/data/filters/sdss_g0.dat"
lines 2 0
read <lamg 1 rg 2>		
data "$!KCORRECT_DIR/data/filters/sdss_r0.dat"
lines 2 0
read <lamr 1 rr 2>		
set lamg = lamg/(1.3)
set lamr = lamr/(1.3)
vecminmax rg rgmin rgmax
set rg = rg/$rgmax
vecminmax rr rrmin rrmax
set rr = rr/$rrmax
limits 2000. 11000. 0. 1.04
angle 90
expand 1.3
axis $fy1 $fy2 0 0 $gx2 $gy1 $($gy2-$gy1) 0 0
relocate 11300. 0.5 
putlabel 5 Band Response (Arbitrary Units)

define yst (0.2)
expand 1.9
angle 0

ctype green
lweight 4
connect lamg rg
define xst (sum(lamg*rg)/sum(rg))
relocate $xst $yst
lweight 1
putlabel 5	^{0.3}g

ctype red
lweight 4
connect lamr rr
define xst (sum(lamr*rr)/sum(rr))
relocate $xst $yst
lweight 1
putlabel 5 	^{0.3}r


